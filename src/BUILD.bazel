package(default_visibility = ["//visibility:public"])

load("@angular//:index.bzl", "ng_module")
load("@build_bazel_rules_typescript//:defs.bzl", "ts_config", "ts_devserver")

ts_config(
    name = "tsconfig-spec",
    src = "tsconfig-spec.json",
    deps = [":tsconfig.json"],
)

ts_config(
    name = "tsconfig-app",
    src = "tsconfig.app.json",
    deps = [":tsconfig.json"],
)

ng_module(
    name = "src",
    srcs = glob(
        ["*.ts"],
        exclude = ["main.ts"],
    ),
    tsconfig = ":tsconfig.json",
    deps = [
        "//src/app",
        "@angular//packages/common/http",
        "@angular//packages/core",
        "@angular//packages/platform-browser",
        "@angular//packages/platform-browser/animations",
        "@angular//packages/router",
        "@angular//packages/service-worker",
        "@npm//:ngx-highlightjs",
        "@npm//:highlight.js",
    ],
)

# This devserver is written in Go and is super-fast.
# It doesn't run any bundler or code splitter. Instead, it concatenates
# UMD JavaScript code on-the-fly in-memory.
# This scales really well for massive codebases.
ts_devserver(
    name = "devserver",
    # serve these files rooted at /
    additional_root_paths = [
        "npm/node_modules/zone.js/dist",
        "npm/node_modules/tslib",
        "npm/node_modules/@angular/material/prebuilt-themes",
    ],
    # Start from the development version of the main
    entry_module = "angular_bazel_example/src/main.dev",
    scripts = [
        ":require.config.js",
        ":module-id.js",
    ],
    # This is the URL we'll point our <script> tag at
    serving_path = "/bundle.min.js",
    # Serve these files in addition to the JavaScript bundle
    static_files = [
        "@npm//node_modules/zone.js:dist/zone.min.js",
        "@npm//node_modules/tslib:tslib.js",
        "@npm//node_modules/@angular/material:prebuilt-themes/deeppurple-amber.css",
        "index.html",
    ],
    # Tell Bazel to build the sources first
    deps = ["//src"],
)

load("@build_bazel_rules_nodejs//:defs.bzl", "nodejs_binary", "rollup_bundle")

rollup_bundle(
    name = "bundle",
    # These Angular routes may be lazy-loaded at runtime.
    # So we tell Rollup that it can put them in separate JS chunks
    # (code-splitting) for faster application startup.
    # In the future, we could automatically gather these from statically
    # analyzing the Angular sources.
    additional_entry_points = [
        "src/app/editor/editor.module.ngfactory",
        "src/app/home/home.module.ngfactory",
    ],
    entry_point = "src/main.prod",
    deps = [
        "//src",
    ],
)

# Copy zone.min.js to this package.
# Needed because the prodserver only loads static files that appear under this
# package.
genrule(
    name = "copy_zonejs",
    srcs = ["@npm//node_modules/zone.js:dist/zone.min.js"],
    outs = ["zone.min.js"],
    cmd = "cp $< $@",
)

# See comment for zonejs above
genrule(
    name = "copy_systemjs",
    srcs = ["@npm//node_modules/systemjs:dist/system.js"],
    outs = ["system.js"],
    cmd = "cp $< $@",
)

# See comment for zonejs above
genrule(
    name = "copy_material_theme",
    srcs = ["@npm//node_modules/@angular/material:prebuilt-themes/deeppurple-amber.css"],
    outs = ["deeppurple-amber.css"],
    cmd = "cp $< $@",
)

load("@build_bazel_rules_nodejs//:defs.bzl", "history_server")

history_server(
    name = "prodserver",
    data = [
        "index.html",
        ":bundle",
        ":copy_material_theme",
        ":copy_systemjs",
        ":copy_zonejs",
    ],
)
